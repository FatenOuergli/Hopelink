#include <M5StickCPlus2.h>
#include <Wire.h>
#include <MAX30100_PulseOximeter.h>
#include <WiFi.h>
#include <ArduinoMqttClient.h>
#include <ArduinoJson.h>

#if __has_include(<esp_sntp.h>)
  #include <esp_sntp.h>
  #define SNTP_ENABLED 1
#elif __has_include(<sntp.h>)
  #include <sntp.h>
  #define SNTP_ENABLED 1
#else
  #define SNTP_ENABLED 0
#endif

// ---------- WiFi ----------
const char* ssid = "ISITcom_wifi_etud";
const char* password = "@isitcom@2022";

// ---------- MQTT ----------
const char* mqtt_server = "192.168.44.99";
const int mqtt_port = 1885;
const char* mqtt_topic = "activity/id/#"; 
int activity_id = 0;
WiFiClient wifiClient;
MqttClient mqttClient(wifiClient);

// ---------- Pulse Sensor ----------
PulseOximeter pox;
#define REPORTING_PERIOD_MS 1000
float bpm = 0.0;
uint32_t tsLastReport = 0;

// ---------- MQTT Publish Timer ----------
unsigned long lastPublish = 0;
const unsigned long publishInterval = 5000;

void setup() {
  Serial.begin(115200);  // Always start Serial first

  auto cfg = M5.config();
  StickCP2.begin(cfg);

  StickCP2.Display.setRotation(1);
  StickCP2.Display.setTextDatum(middle_center);
  StickCP2.Display.setFont(&fonts::FreeSansBold9pt7b);
  StickCP2.Display.setTextSize(1);
  StickCP2.Display.setTextColor(GREEN);
  StickCP2.Display.setCursor(60, 10);
  StickCP2.Display.println("Heart Rate");

  // ---------- WiFi ----------
  WiFi.begin(ssid, password);
  Serial.print("Connecting to WiFi");
  while (WiFi.status() != WL_CONNECTED) {
    delay(500);
    Serial.print(".");
  }
  Serial.println("\nWiFi connected");

  // ---------- RTC Check & SNTP Sync ----------
  if (!StickCP2.Rtc.isEnabled()) {
    Serial.println("RTC not found.");
  }

#if SNTP_ENABLED
  configTzTime("UTC+2", "pool.ntp.org");
  struct tm timeinfo;
  if (getLocalTime(&timeinfo)) {
    m5::rtc_time_t t;
    t.hours = timeinfo.tm_hour;
    t.minutes = timeinfo.tm_min;
    t.seconds = timeinfo.tm_sec;
    StickCP2.Rtc.setTime(t);
    Serial.printf("RTC synced via NTP: %02d:%02d:%02d\n", t.hours, t.minutes, t.seconds);
  } else {
    Serial.println("SNTP sync failed.");
  }
#endif

  // ---------- MQTT ----------
  mqttClient.setId("m5stickcplus2");
  mqttClient.onMessage(onMqttMessage);

  if (!mqttClient.connect(mqtt_server, mqtt_port)) {
    Serial.println("MQTT connection failed!");
    while (true);  // stop everything
  }
  Serial.println("MQTT connected");
  mqttClient.subscribe(mqtt_topic);

  // ---------- Pulse Sensor ----------
  if (!pox.begin()) {
    Serial.println("Failed to initialize MAX30100");
    while (1);
  }
  Serial.println("MAX30100 ready");
  pox.setIRLedCurrent(MAX30100_LED_CURR_7_6MA);
}

void loop() {
  mqttClient.poll();
  pox.update();

  float accX = 0, accY = 0, accZ = 0;
  float gyrX = 0, gyrY = 0, gyrZ = 0;

  if (activity_id > 0) {
    if (StickCP2.Imu.update()) {
      auto imu = StickCP2.Imu.getImuData();
      accX = imu.accel.x;
      accY = imu.accel.y;
      accZ = imu.accel.z;
      gyrX = imu.gyro.x;
      gyrY = imu.gyro.y;
      gyrZ = imu.gyro.z;
    }

    // ---------- Update BPM + Display ----------
    if (millis() - tsLastReport > REPORTING_PERIOD_MS) {
      bpm = pox.getHeartRate();
      Serial.print("BPM: ");
      Serial.println(bpm);

      StickCP2.Display.fillScreen(BLACK);

      // Time - top left
      m5::rtc_time_t time = StickCP2.Rtc.getTime();
      StickCP2.Display.setTextColor(WHITE);
      StickCP2.Display.setCursor(0, 10);
      StickCP2.Display.printf("%02d:%02d", time.hours, time.minutes);

      // Battery - top right
      int battery = StickCP2.Power.getBatteryLevel();
      StickCP2.Display.setCursor(180, 10);
      StickCP2.Display.printf("%d%%", battery);

      // BPM - center
      StickCP2.Display.setTextDatum(middle_center);
      StickCP2.Display.setCursor(60, 50);
      if (bpm < 10.0) {
        StickCP2.Display.setTextColor(RED);
        StickCP2.Display.print("No pulse");
      } else {
        StickCP2.Display.setTextColor(GREEN);
        StickCP2.Display.printf("BPM: %.0f", bpm);  // Rounded integer
      }

      tsLastReport = millis();
    }

    // ---------- Publish MQTT ----------
    if (millis() - lastPublish > publishInterval) {
      StaticJsonDocument<256> doc;
      doc["heart_rate"] = bpm;
      doc["accelerometer_x"] = accX;
      doc["accelerometer_y"] = accY;
      doc["accelerometer_z"] = accZ;
      doc["gyroscope_x"] = gyrX;
      doc["gyroscope_y"] = gyrY;
      doc["gyroscope_z"] = gyrZ;
      doc["activity"] = activity_id;

      String json;
      serializeJson(doc, json);
      char buffer[100];
      sprintf(buffer, "data/%d", activity_id);
      mqttClient.beginMessage(buffer);
      mqttClient.print(json);
      mqttClient.endMessage();
      Serial.println(buffer);
      Serial.println("Published to MQTT: " + json);
      lastPublish = millis();
    }
  }

  delay(10);  // smooth loop
}

// ---------- MQTT Message Handler ----------
void onMqttMessage(int messageSize) {
  String topic = mqttClient.messageTopic();
  String message = "";
  while (mqttClient.available()) {
    message += (char)mqttClient.read();
  }
  Serial.println("Received [" + topic + "]:" + message);
  StaticJsonDocument<256> doc;
  deserializeJson(doc, message);
  activity_id = doc["activity"];
  Serial.println(activity_id);
}
